(ns hoplon.google.jsapi.maps
  (:refer-clojure :exclude [clj->js])
  (:require
   [clojure.string :as string]
   [hoplon.google.jsapi.loader :refer [queued ensure-api]]))

(defn clj->js
  "Recursively transforms ClojureScript maps into Javascript objects,
   other ClojureScript colls into JavaScript arrays, and ClojureScript
   keywords into JavaScript strings."
  [x]
  (cond
    (string? x)  x
    (keyword? x) (name x)
    (map? x) (with-let [obj (js-obj)]
               (doseq [[k v] x]
                 (aset obj (clj->js k) (clj->js v))))
    (or (seq? x) (vector? x)) (apply array (map clj->js x))
    :else x))

(defc  maps-version "3")
(defc  maps-options {:other_params "sensor=false"})

(def ensure-maps
  (queued
    (fn [callback]
      (ensure-api
        #(.load js/google "maps" @maps-version
           (clj->js (assoc @maps-options :callback callback)))))))

(defn- dom2str [elem] (.-innerHTML (div elem)))
(defn- indexed [coll] (map-indexed vector coll))

(defn delay-until [ready? f & args]
  #(cell= (when ready? (~(memoize (fn [] (or (apply f args) ::ok)))))))

(defn starts-with? [string prefix]
  (= 0 (.lastIndexOf string prefix 0)))

(defn visible? [elem]
  (with-let [c (cell nil)]
    (with-interval 100 (reset! c (.is (js/jQuery elem) ":visible")))))

(defn point-in-polygon [point path]
  (let [maps              (.. js/google -maps)
        LatLng            (.-LatLng maps)
        Polygon           (.-Polygon maps)
        poly-lib          (.-poly (.-geometry maps))
        lat-lng           #(LatLng. (js/parseFloat %1) (js/parseFloat %2))
        gon               (Polygon. (clj->js {:path (mapv #(lat-lng (:lat %) (:lon %)) path)})) 
        pnt               (lat-lng (:lat point) (:lon point))
        res               (.containsLocation poly-lib pnt gon)]
    res))

(defmulti layer (fn [x y imap] x))

(defmethod layer :hoplon.google.jsapi.maps/layer-bicycling [x y imap]
  (let [maps          (.. js/google -maps)
        Bicycling     (.-BicyclingLayer maps)
        bicycling     (Bicycling.)]
    (cell= (let [map      (when y imap)]
             (.setMap bicycling map)))))

(defmethod layer :hoplon.google.jsapi.maps/layer-traffic [x y imap]
  (let [maps          (. js/google -maps)
        Traffic       (.-TrafficLayer maps)
        traffic       (Traffic.)]
    (cell= (let [map      (when y imap)]
             (.setMap traffic map)))))

(defmethod layer :hoplon.google.jsapi.maps/layer-transit [x y imap]
  (let [maps          (. js/google -maps)
        Transit       (.-TransitLayer maps)
        transit       (Transit.)]
    (cell= (let [map      (when y imap)]
             (.setMap transit map)))))

(defelem google-map [attr _]
  (with-let [elem       (div (select-keys attr (keys (filter #(not= "hoplon.google.jsapi.maps" (namespace (key %))) attr))))]
    (let [visible?      (visible? elem)
          center        (:hoplon.google.jsapi.maps/center attr)
          map-opts      (:hoplon.google.jsapi.maps/opts attr)
          fit-pins      (:hoplon.google.jsapi.maps/fit-pins attr)
          markers       (:hoplon.google.jsapi.maps/markers attr)
          polylines     (:hoplon.google.jsapi.maps/polylines attr)
          polygons      (:hoplon.google.jsapi.maps/polygons attr)
          mkfilter      #(fn [[x _]]
                           (let [ns (namespace x) nm (name x)]
                             (and (= "hoplon.google.jsapi.maps" ns)
                               (starts-with? nm %))))
          layers        (filter (mkfilter "layer-") attr)
          map-callbacks (filter (mkfilter "map-") attr)
          pln-callbacks (filter (mkfilter "polyline-") attr)
          pgn-callbacks (filter (mkfilter "polygon-") attr)
          pin-callbacks (filter (mkfilter "marker-") attr)
          rm-pfx        #(string/replace % #"^[^-]+-" "")]
      (ensure-maps
        (delay-until visible?
          (fn []
            (let [maps         (.. js/google -maps)
                  Map          (.-Map maps)
                  Event        (.-event maps)
                  LatLng       (.-LatLng maps)
                  Marker       (.-Marker maps)
                  Polyline     (.-Polyline maps)
                  Polygon      (.-Polygon maps)
                  InfoWindow   (.-InfoWindow maps)
                  LatLngBounds (.-LatLngBounds maps)
                  lat-lng      #(LatLng. (js/parseFloat %1) (js/parseFloat %2))
                  opts         (cell= (let [{:keys [lat lon]} center]
                                        (clj->js (merge {} map-opts {:center (lat-lng lat lon)}))))
                  imap         (Map. elem @opts)

                  bounds       (cell= (with-let [b (LatLngBounds.)]
                                        (doseq [{:keys [lat lon]} markers]
                                          (.extend b (lat-lng lat lon)))))]
              (doseq [x map-callbacks]
                (let [evt (rm-pfx (name (key x))) 
                      fun (val x)
                      wrp (fn [& args] (apply fun imap args)) ]
                  (.addListener Event imap evt wrp)))
              (mapv #(layer (key %) (val %) imap) layers)
              (cell-doseq [[i {:keys [path opts] :as pline}] (cell= (indexed polylines))]
                (let [polyline  (Polyline. (clj->js {}))]
                  (doseq [x pln-callbacks]
                    (let [evt (rm-pfx (name (key x))) 
                          fun (val x)
                          wrp (fn [& args] (apply fun imap polyline pline args))]
                      (.addListener Event polyline evt wrp)))
                  (cell= (let [map  (when path imap)
                               path (if path (mapv #(lat-lng (:lat %) (:lon %)) path) [])
                               opt  (clj->js (merge {} opts {:map map :path path}))]
                           (.setOptions polyline opt)))))
              (cell-doseq [[i {:keys [path opts] :as pgon}] (cell= (indexed polygons))]
                (let [ polygon (Polygon. (clj->js {}))]
                  (doseq [x pgn-callbacks]
                    (let [evt (rm-pfx (name (key x))) 
                          fun (val x)
                          wrp (fn [& args] (apply fun imap polygon pgon args))]
                      (.addListener Event polygon evt wrp)))
                  (cell= (let [map  (when path imap)
                               path (if path (mapv #(lat-lng (:lat %) (:lon %)) path) [])
                               opt  (clj->js (merge {} opts {:map map :path path}))]
                           (.setOptions polygon opt)))))
              (cell= (.setOptions imap opts))
              (cell= (when (and fit-pins (seq markers))
                       ((~(partial delay-until visible?)
                         #(.fitBounds imap bounds)))))
              (cell-doseq [[i {:keys [lat lon info opts] :as pin}] (cell= (indexed markers))]
                (let [marker  (Marker. (clj->js {}))
                      iwindow (InfoWindow. (clj->js {}))]
                  (doseq [x pin-callbacks]
                    (let [evt  (rm-pfx (name (key x))) 
                          fun  (val x)
                          wrp  (fn [& args] (apply fun imap marker iwindow @pin args))]
                      (.addListener Event marker evt wrp)))
                  (.addListener Event marker "click"
                    (fn []
                      (when @info (.open iwindow imap marker))))
                  (cell= (doto iwindow (.setContent (dom2str info)) .close))
                  (cell= (let [map (when lat imap)
                               pos (when lat (lat-lng lat lon))
                               opt (clj->js (merge {} opts {:map map :position pos}))]
                           (.close iwindow)
                           (.setOptions marker opt))))))))))))
